const VAR_ENABLE_LITTLE_KID_EVENT = VAR_TEMP_0

mapscripts Custom_Hoenn_LittleRoot_MapScripts {
    # https://github.com/pret/pokeemerald/wiki/The-Basics-of-Scripting#Map-Scripts
    MAP_SCRIPT_ON_LOAD {
        setvar(VAR_ENABLE_LITTLE_KID_EVENT, FALSE)
        if(!flag(FLAG_SET_WALL_CLOCK)) {
            setvar(VAR_ENABLE_LITTLE_KID_EVENT, TRUE)
        }
    }
}

script Custom_LittleRoot_PlayerReflection {
    playbgm(MUS_DUMMY, FALSE)
    applymovement(LOCALID_PLAYER_REFLECTION, moves(
        face_player
    ))
    msgbox(Text_Custom_LittleRoot_PlayerReflection, MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_PLAYER_REFLECTION, moves(
        face_up
    ))
    playbgm(MUS_LITTLEROOT, FALSE)
    setflag(FLAG_CUSTOM_HOENN_LITTLEROOT_HIDE_REFLECTION)
}

script Custom_LittleRoot_LittleKid_Event_Left {
    lockall
    applymovement(LOCALID_LITTLEKID, moves(
        face_right
        emote_exclamation_mark
    ))
    playse(SE_PIN)
    call(Custom_LittleRoot_LittleKid)
    applymovement(LOCALID_LITTLEKID, moves(
        face_down
    ))
    applymovement(LOCALID_PLAYER, moves(
        walk_down
    ))
    waitmovement
    releaseall
}

script Custom_LittleRoot_LittleKid_Event_Right {
    lockall
    applymovement(LOCALID_LITTLEKID, moves(
        face_right
        emote_exclamation_mark
        walk_right
    ))
    playse(SE_PIN)
    waitmovement
    call(Custom_LittleRoot_LittleKid)
    applymovement(LOCALID_LITTLEKID, moves(
        walk_left
        face_down
    ))
    applymovement(LOCALID_PLAYER, moves(
        walk_down
    ))
    waitmovement
    releaseall
}

script Custom_LittleRoot_LittleKid {
    applymovement(LOCALID_LITTLEKID, moves(
        face_player
    ))
    if(var(VAR_ENABLE_LITTLE_KID_EVENT)) {
        msgbox(Text_Custom_LittleRoot_LittleKid_Event, MSGBOX_AUTOCLOSE)
    } else {
        msgbox(Text_Custom_LittleRoot_LittleKid, MSGBOX_AUTOCLOSE)
    }
}

script Custom_LittleRoot_PlayerHouseSign {
    msgbox(Text_Custom_LittleRoot_Sign_Player_House, MSGBOX_AUTOCLOSE)
}

script Custom_LittleRoot_RivalHouseSign {
    msgbox(Text_Custom_LittleRoot_Sign_Rival_House, MSGBOX_AUTOCLOSE)
}

script Custom_LittleRoot_RivalHouseDoor {
    msgbox(Text_Custom_LittleRoot_Rival_House_Door, MSGBOX_AUTOCLOSE)
}

script Custom_LittleRoot_LabSign {
    msgbox(Text_Custom_LittleRoot_Sign_Lab, MSGBOX_AUTOCLOSE)
}

text Text_Custom_LittleRoot_PlayerReflection {
    format("You remember how this goes, don't you?")
}

text Text_Custom_LittleRoot_LittleKid {
    format("You're late again… weren't you?\nNo… I think you were early.")
}

text Text_Custom_LittleRoot_LittleKid_Event {
    format(
        "The clock in your room isn't right.\p
        It stopped after you left… You should fix it."
    )
}

text Text_Custom_LittleRoot_Sign_Lab {
    format("The sign says: 'Pokémon Lab.' But you're not sure that's what it really is.")
}

text Text_Custom_LittleRoot_Sign_Player_House {
    format("The sign says that… you live here? That can't be right.")
}

text Text_Custom_LittleRoot_Sign_Rival_House {
    format("The text in the sign is smudged.")
}

text Text_Custom_LittleRoot_Rival_House_Door {
    format("The handle is cold. You let go.")
}
