const VAR_SHOULD_TRIGGER_ROCKET_EVENT = VAR_TEMP_0
const VAR_IS_PLAYER_ON_THE_RIGHT = VAR_TEMP_1

mapscripts Custom_Hoenn_Route101_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_SHOULD_TRIGGER_ROCKET_EVENT, FALSE)
        if(!flag(FLAG_CUSTOM_INTRO_ROCKET_DEFEATED)) {
            setvar(VAR_SHOULD_TRIGGER_ROCKET_EVENT, TRUE) # Needed as triggers need a variable and don't work with a flag
        }
    }
}

script Custom_Route101_MartEmployee {
    lock
    applymovement(LOCAL_ID_MARTEMPLOYEE, moves(
        face_player
    ))
    if (!flag(FLAG_CUSTOM_RECEIVED_SAMPLE_ROUTE_101)) {
        msgbox(Text_Custom_Route101_MartEmployee1, MSGBOX_AUTOCLOSE)
        giveitem(ITEM_POKE_BALL, 20)
        # giving item resets lock on NPC so if it has LOOK_AROUND it will start looking around again
        applymovement(LOCAL_ID_MARTEMPLOYEE, moves(
            face_player
        ))
        setflag(FLAG_CUSTOM_RECEIVED_SAMPLE_ROUTE_101)
    }
    msgbox(Text_Custom_Route101_MartEmployee2, MSGBOX_AUTOCLOSE)
    release
}

script Custom_Route101_Rocket_Left {
    setvar(VAR_IS_PLAYER_ON_THE_RIGHT, FALSE)
    call(Custom_Route101_Rocket)
}

script Custom_Route101_Rocket_Right {
    setvar(VAR_IS_PLAYER_ON_THE_RIGHT, TRUE)
    call(Custom_Route101_Rocket)
}

script(local) Custom_Route101_Rocket {
    lockall
    setvar(VAR_SHOULD_TRIGGER_ROCKET_EVENT, FALSE) # Prevent from triggering again
    
    delay(20)
    applymovement(LOCALID_ROCKET, moves(
        face_down
    ))
    waitmovement
    delay(20)
    applymovement(LOCALID_PLAYER, moves(
        face_up,
        emote_exclamation_mark,
    ))
    playse(SE_PIN)
    waitmovement

    playbgm(MUS_RG_ENCOUNTER_ROCKET, FALSE)
    
    applymovement(LOCALID_ROCKET, moves(
        walk_down * 3
    ))
    waitmovement
    if(var(VAR_IS_PLAYER_ON_THE_RIGHT)) {
        applymovement(LOCALID_ROCKET, moves(
            walk_down,
            walk_in_place_fast_right,
            face_player,
        ))
        waitmovement
        applymovement(LOCALID_PLAYER, moves(
            walk_in_place_fast_left,
        ))
        waitmovement
    }
    msgbox(Text_Custom_Intro_RocketIntro, MSGBOX_AUTOCLOSE)
    
    trainerbattle_no_intro(TRAINER_INTRO_1, Text_Custom_Intro_RocketDefeat)

    msgbox(Text_Custom_Intro_RocketDefeat2, MSGBOX_AUTOCLOSE)
    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_CUSTOM_INTRO_ROCKET_DEFEATED)
    playse(SE_EXIT)
    removeobject(LOCALID_ROCKET)
    delay(10)
    fadescreen(FADE_FROM_BLACK)
    releaseall
}

text Text_Custom_Intro_RocketIntro {
    format(
        "Hey... I was supposed to be stealing Pokémon for my boss, but I got lost.\p
        This place... it's not like anything I've seen before.\p
        Maybe you can help me find my way out? I don't want to get caught wandering here forever\p
        ...\n
        ...\p
        Wait a minute. You're a trainer, aren't you?\p
        Well, then... I guess I don't need  your help. I'm here to take your Pokémon too!"
    )
}

text Text_Custom_Intro_RocketDefeat {
    format(
        "Aren't you too strong for a kid? You're better than I thought."
    )
}

text Text_Custom_Intro_RocketDefeat2 {
    format(
        "Nevermind, I'll find the way out on my own.\p
        Don't think this is over. The Team Rocket always gets what they want!"
    )
}

text Text_Custom_Route101_MartEmployee1 {
    format(
        "Oh! Finally! A potential customer! I haven't seen any in a long time.\p
        Here, take this. On the house!"
    )
}

text Text_Custom_Route101_MartEmployee2 {
    format(
        "When you get the chance, check out one of our stores! We're always open!"
    )
}